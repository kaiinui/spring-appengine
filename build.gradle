buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.springsource.org/plugins-release" }
    }
    dependencies {
        classpath "org.gradle.api.plugins:gradle-gae-plugin:0.8"
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.2"
    }
}

ext.appengineVersion = "1.8.1"
ext.junitVersion    = "4.11"
ext.springVersion = "3.2.3.RELEASE"

configure(allprojects) { project ->

    group = "com.github.marceloverdijk"

    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "propdeps"

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
        maven { url "http://repo.springsource.org/libs-snapshot" }
    }

    dependencies {
        compile "com.google.appengine:appengine-api-1.0-sdk:$appengineVersion"
        compile "org.springframework:spring-core:$springVersion"
        compile "org.springframework:spring-context:$springVersion"
        compile "org.springframework:spring-context-support:$springVersion"
        testCompile("com.google.appengine:appengine-api-labs:${appengineVersion}")
        testCompile("com.google.appengine:appengine-api-stubs:${appengineVersion}")
        testCompile("com.google.appengine:appengine-testing:${appengineVersion}")
        testCompile("junit:junit:${junitVersion}")
    }

    jar {
        manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
        manifest.attributes["Implementation-Title"] = project.name
        manifest.attributes["Implementation-Version"] = project.version
        from("${rootProject.projectDir}/src/dist") {
            include "LICENSE"
            include "NOTICE"
            into "META-INF"
            expand(copyright: new Date().format("yyyy"), version: project.version)
        }
    }

    javadoc {
        options.author = true
        options.header = project.name
        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

def samples = subprojects.findAll { project -> project.path.contains("samples") }

configure(samples) {

    apply plugin: "gae"
    apply plugin: "war"

    dependencies {
        compile rootProject
        compile "org.springframework:spring-aspects:$springVersion"
        compile "org.springframework:spring-beans:$springVersion"
        compile "org.springframework:spring-webmvc:$springVersion"
        gaeSdk "com.google.appengine:appengine-java-sdk:$appengineVersion"
    }

    gae {
        disableUpdateCheck = true
        downloadSdk = true
        stopPort = 8090
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.6"
}
