buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.springsource.org/plugins-release" }
    }
    dependencies {
        classpath "org.gradle.api.plugins:gradle-gae-plugin:0.8"
        classpath "org.gradle.api.plugins:gradle-nexus-plugin:0.3"
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.2"
    }
}

ext.appengineVersion = "1.8.5"
ext.junitVersion = "4.11"
ext.objectifyVersion = "4.0rc1"
ext.springVersion = "3.2.4.RELEASE"

configure(allprojects) { project ->

    group = "com.googlecode.spring-appengine"
    version = "0.1"

    apply plugin: "java"
    apply plugin: "nexus"
    apply plugin: "propdeps"
    apply plugin: "propdeps-eclipse"
    apply plugin: "propdeps-idea"
    apply plugin: "propdeps-maven"
    apply plugin: "signing"

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
        maven { url "http://repo.springsource.org/libs-snapshot" }
    }

    dependencies {
        provided "com.google.appengine:appengine-api-1.0-sdk:$appengineVersion"
        provided "com.google.appengine:appengine-api-labs:${appengineVersion}"
        provided "com.google.appengine:appengine-api-stubs:${appengineVersion}"
        provided "com.google.appengine:appengine-testing:${appengineVersion}"
        provided "com.googlecode.objectify:objectify:$objectifyVersion"
        provided "junit:junit:${junitVersion}"
        provided "org.springframework:spring-core:$springVersion"
        provided "org.springframework:spring-context:$springVersion"
        provided "org.springframework:spring-context-support:$springVersion"
    }

    jar {
        manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
        manifest.attributes["Implementation-Title"] = project.name
        manifest.attributes["Implementation-Version"] = project.version
        from("${rootProject.projectDir}/src/dist") {
            include "LICENSE"
            include "NOTICE"
            into "META-INF"
            expand(copyright: new Date().format("yyyy"), version: project.version)
        }
    }

    javadoc {
        options.author = true
        options.header = project.name
        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    }
}

def samples = subprojects.findAll { project -> project.path.contains("samples") }

configure(samples) {

    apply plugin: "gae"
    apply plugin: "war"

    dependencies {
        compile rootProject
        compile "org.springframework:spring-aspects:$springVersion"
        compile "org.springframework:spring-beans:$springVersion"
        compile "org.springframework:spring-webmvc:$springVersion"
        gaeSdk "com.google.appengine:appengine-java-sdk:$appengineVersion"
    }

    gae {
        disableUpdateCheck = true
        downloadSdk = true
        stopPort = 8090
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.7"
}
