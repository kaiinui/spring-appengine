buildscript {
    repositories {
        mavenCentral()
        maven { url "http://jcenter.bintray.com" }
        maven { url "http://repo.springsource.org/plugins-release" }
    }
    dependencies {
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:0.7.0"
        classpath "org.gradle.api.plugins:gradle-gae-plugin:0.8"
        classpath "org.gradle.api.plugins:gradle-nexus-plugin:0.3"
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.2"
    }
}

apply plugin: "asciidoctor"
apply plugin: "java"
apply plugin: "nexus"
apply plugin: "propdeps"
apply plugin: "propdeps-eclipse"
apply plugin: "propdeps-idea"
apply plugin: "propdeps-maven"

group = "com.googlecode.spring-appengine"
version = "0.2-BUILD-SNAPSHOT"

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext.appEngineVersion = "1.8.6"
ext.commonsLoggingVersion = "1.1.1"
ext.jspVersion = "2.1"
ext.junitVersion = "4.11"
ext.objectifyVersion = "4.0rc2"
ext.servletVersion = "2.5"
ext.springVersion = "3.2.4.RELEASE"
ext.springDataVersion = "1.6.1.RELEASE"

repositories {
    mavenCentral()
    maven { url "http://repo.springsource.org/libs-snapshot" }
}

dependencies {
    provided "com.google.appengine:appengine-api-1.0-sdk:${appEngineVersion}"
    testCompile "com.google.appengine:appengine-api-labs:${appEngineVersion}"
    testCompile "com.google.appengine:appengine-api-stubs:${appEngineVersion}"
    testCompile "com.google.appengine:appengine-testing:${appEngineVersion}"
    optional "com.googlecode.objectify:objectify:${objectifyVersion}"
    testCompile "junit:junit:${junitVersion}"
    compile  "commons-logging:commons-logging:${commonsLoggingVersion}"
    provided "javax.servlet:servlet-api:${servletVersion}"
    provided "javax.servlet.jsp:jsp-api:${jspVersion}"
    optional "org.springframework:spring-core:${springVersion}"
    optional "org.springframework:spring-context:${springVersion}"
    optional "org.springframework:spring-context-support:${springVersion}"
    optional "org.springframework:spring-web:${springVersion}"
    optional "org.springframework.data:spring-data-commons:${springDataVersion}"
}

asciidoctor {
    options = [
        eruby: "erubis",
        attributes: [
            'source-highlighter': 'prettify',
            sectanchors : "",
            toc2: "",
            idprefix: "",
            idseparator: "-"
        ]
    ]
}

jar {
    manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
    manifest.attributes["Implementation-Title"] = project.name
    manifest.attributes["Implementation-Version"] = project.version
    from("${rootProject.projectDir}/src/dist") {
        include "LICENSE"
        include "NOTICE"
        into "META-INF"
        expand(copyright: new Date().format("yyyy"), version: project.version)
    }
}

javadoc {
    options.author = true
    options.header = project.name
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    options.links([
        "http://developers.google.com/appengine/docs/java/javadoc/",
        "http://docs.objectify-appengine.googlecode.com/git/apidocs/",
        "http://docs.oracle.com/javaee/6/api/",
        "http://docs.oracle.com/javase/7/docs/api/",
        "http://docs.spring.io/spring/docs/3.2.4.RELEASE/javadoc-api/"
    ] as String[])
}

modifyPom {
    project {
        name "Spring App Engine"
        description "Spring App Engine Integration."
        packaging "jar"
        url "https://github.com/marceloverdijk/spring-appengine"
        inceptionYear "2013"

        scm {
            url "https://github.com/marceloverdijk/spring-appengine"
            connection "scm:https://marceloverdijk@github.com/marceloverdijk/spring-appengine.git"
            developerConnection "scm:git://github.com/marceloverdijk/spring-appengine.git"
        }

        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution "repo"
            }
        }

        developers {
            developer {
                id "marceloverdijk"
                name "Marcel Overdijk"
                email "marcel@overdijk.me"
            }
        }
    }
}

configure([test]) {
    systemProperties['springAppengineVersion'] = version
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.8"
}
